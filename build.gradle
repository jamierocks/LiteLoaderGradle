buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name = 'minecrell'
            url = 'http://repo.minecrell.net/snapshots'
        }
    }
    dependencies {
        classpath 'net.minecrell:VanillaGradle:1.0.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecrell.vanilla.base'
apply plugin: 'maven-publish'

group = 'com.mumfrey'
archivesBaseName = 'LiteLoader'
version = '1.8.0_00'

ext.author = 'Mumfrey'

sourceCompatibility = 1.6
targetCompatibility = 1.6

minecraft {
    version = '1.8'
    mappings = 'snapshot_20150527'
}

dependencies {
    compile 'org.ow2.asm:asm-debug-all:5.0.3'
    compile 'net.minecraft:launchwrapper:1.11'
}

sourceSets {
    common {
        compileClasspath += main.compileClasspath
        java {
            srcDir 'LiteLoader/LiteLoader/trunk/java/common'
        }
        resources {
            srcDir 'LiteLoader/LiteLoader/trunk/resources'
        }
    }
    client {
        compileClasspath += common.compileClasspath + common.output
        java {
            srcDir 'LiteLoader/LiteLoader/trunk/java/client'
        }
    }
    debug {
        compileClasspath += client.compileClasspath + client.output
        java {
            srcDir 'LiteLoader/LiteLoader/trunk/debug'
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:none', '-XDignore.symbol.file']
}

ext.manifest = [
        'Built-By': 'ForgeGradle (http://minecraftforge.net)',
        'Implementation-Vendor': author,
        'Implementation-Title': archivesBaseName,
        'Implementation-Version': version,
        'TweakClass': 'com.mumfrey.liteloader.launch.LiteLoaderTweaker',
        'TweakName': archivesBaseName,
        'TweakVersion': version,
        'TweakAuthor': author
]

jar {
    from sourceSets.common.output
    from sourceSets.client.output

    manifest {
        attributes project.manifest
    }
}

task jarMcp(type:Jar) {
    from sourceSets.common.output
    from sourceSets.client.output
    from sourceSets.debug.output

    manifest {
        attributes project.manifest
    }

    classifier = 'mcpnames'
}

task jarSrc(type:Jar) {
    from sourceSets.common.allSource
    from sourceSets.client.allSource
    from sourceSets.debug.allSource

    classifier = 'sources'
}

javadoc {
    source sourceSets.common.allJava
    source sourceSets.client.allJava
    failOnError = false
}

task jarDoc(type:Jar, dependsOn: javadoc) {
    from javadoc.destinationDir

    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact jarMcp

            repositories {
                maven {
                    url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?:
                            "$buildDir/repo"
                }
            }
        }
    }
}

artifacts.archives jarMcp, jarSrc, jarDoc

reobf.setSrgMcp()

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
